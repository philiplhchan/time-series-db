/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 */
import org.opensearch.gradle.test.RestIntegTestTask

buildscript {
    ext {
        opensearch_version = System.getProperty("opensearch.version", "3.5.0-SNAPSHOT")
        version_qualifier = System.getProperty("build.version_qualifier", "")
        buildVersionQualifier = System.getProperty("build.version_qualifier", "")
        isSnapshot = "true" == System.getProperty("build.snapshot", "true")
        version_tokens = opensearch_version.tokenize('-')
        opensearch_build = version_tokens[0] + '.0'
        plugin_no_snapshot = opensearch_build
        if (buildVersionQualifier) {
            opensearch_build += "-${buildVersionQualifier}"
            plugin_no_snapshot += "-${buildVersionQualifier}"
        }
        if (isSnapshot) {
            opensearch_build += "-SNAPSHOT"
        }
        opensearch_group = "org.opensearch"
        opensearch_no_snapshot = opensearch_build.replace("-SNAPSHOT","")
    }
    repositories {
        mavenLocal()
        maven {
            url "https://ci.opensearch.org/ci/dbc/snapshots/maven/"
            content {
                excludeGroupByRegex "com\\.fasterxml\\.jackson.*"
            }
        }
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "${opensearch_group}.gradle:build-tools:${opensearch_version}"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:8.1.0"
    }
}

allprojects {
    group = 'org.opensearch.plugin'
    version = opensearch_version.tokenize('-')[0] + '.0'
    if (buildVersionQualifier) {
        version += "-${buildVersionQualifier}"
    }
    if (isSnapshot) {
        version += "-SNAPSHOT"
    }

    plugins.withId('java') {
        targetCompatibility = JavaVersion.VERSION_21
        sourceCompatibility = JavaVersion.VERSION_21
    }
}

repositories {
    mavenLocal()
    maven {
        url "https://ci.opensearch.org/ci/dbc/snapshots/maven/"
        content {
            excludeGroupByRegex "com\\.fasterxml\\.jackson.*"
        }
    }
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

apply plugin: 'opensearch.opensearchplugin'
apply plugin: 'opensearch.java-agent'
apply plugin: 'com.diffplug.spotless'
apply plugin: 'opensearch.pluginzip'
apply from: 'gradle/formatting.gradle'

opensearchplugin {
    description = 'Plugin for TSDB'
    classname = 'org.opensearch.tsdb.TSDBPlugin'
    licenseFile rootProject.file('LICENSE.txt')
    noticeFile rootProject.file('NOTICE.txt')
}

publishing {
    repositories {
        maven {
            name = "Snapshots"
            url = System.getenv("MAVEN_SNAPSHOTS_S3_REPO")
            credentials(AwsCredentials) {
                accessKey = System.getenv("AWS_ACCESS_KEY_ID")
                secretKey = System.getenv("AWS_SECRET_ACCESS_KEY")
                sessionToken = System.getenv("AWS_SESSION_TOKEN")
            }
        }
    }
    publications {
        pluginZip(MavenPublication) { publication ->
            pom {
                name = "tsdb"
                description = "OpenSearch plugin for TSDB"
                groupId = "org.opensearch.plugin"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        name = "OpenSearch"
                        // TODO to add
                        url = "https://github.com/opensearch-project/opensearch-tsdb"
                    }
                }
            }
        }
    }
}

spotless {
  java {
    licenseHeaderFile 'formatter/license-header.txt'
    targetExclude '**/package-info.java', '**/generated/**'
  }
}

// This requires an additional Jar not published as part of build-tools
loggerUsageCheck.enabled = false

afterEvaluate {
    project.validateNebulaPom.enabled = false
}

// Configure javaRestTest source set
sourceSets {
    javaRestTest {
        java {
            srcDirs = ['src/javaRestTest/java']
        }
        resources {
            srcDirs = ['src/javaRestTest/resources']
        }
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }

    internalClusterTest {
        java {
            srcDirs = ['src/internalClusterTest/java']
        }
        resources {
            srcDirs = ['src/internalClusterTest/resources']
        }
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

configurations {
    javaRestTestImplementation.extendsFrom testImplementation
    javaRestTestRuntimeOnly.extendsFrom testRuntimeOnly

    internalClusterTestImplementation.extendsFrom testImplementation
    internalClusterTestRuntimeOnly.extendsFrom testRuntimeOnly

    // Resolve dependency conflicts
    all {
        resolutionStrategy {
            force 'org.yaml:snakeyaml:2.5'
            // Force Arrow 18.1.0 to avoid version conflicts
            force 'org.apache.arrow:arrow-vector:18.1.0'
            force 'org.apache.arrow:arrow-memory-core:18.1.0'
            force 'org.apache.arrow:arrow-memory-netty:18.1.0'
            force 'org.apache.arrow:arrow-memory-netty-buffer-patch:18.1.0'
            force 'org.apache.arrow:arrow-format:18.1.0'
        }
        // Exclude arrow-memory-unsafe to avoid conflict with arrow-memory-netty
        // Both provide DefaultAllocationManagerFactory
        exclude group: 'org.apache.arrow', module: 'arrow-memory-unsafe'
    }
}

task integTest(type: RestIntegTestTask) {
    description = "Run tests against a cluster"
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
}

integTest {
    // The --debug-jvm command-line option makes the cluster debuggable; this makes the tests debuggable
    if (System.getProperty("test.debug") != null) {
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5005'
    }
}

task javaRestTest(type: RestIntegTestTask) {
    description = "Run REST integration tests"
    group = JavaBasePlugin.VERIFICATION_GROUP
    testClassesDirs = sourceSets.javaRestTest.output.classesDirs
    classpath = sourceSets.javaRestTest.runtimeClasspath
    mustRunAfter test
}

javaRestTest {
    // The --debug-jvm command-line option makes the cluster debuggable; this makes the tests debuggable
    if (System.getProperty("test.debug") != null) {
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5005'
    }
}

task internalClusterTest(type: Test) {
    description = "Run internal cluster integration tests"
    group = JavaBasePlugin.VERIFICATION_GROUP
    testClassesDirs = sourceSets.internalClusterTest.output.classesDirs
    classpath = sourceSets.internalClusterTest.runtimeClasspath
    mustRunAfter test, javaRestTest

    /**
     * During peer recovery (primary relocation, adding replicas, or recovering failed replicas),
     * the recovering shard may receive the same operation twice due to a race window in the recovery:
     *
     * <p>Recovery timeline:
     * <pre>
     * 1. initiateTracking() called â†’ recovering shard joins replication group
     * 2. New operations now replicate to recovering shard (REPLICA origin)
     * 3. endingSeqNo sampled from primary's maxSeqNo
     * 4. Phase 2 sends translog ops [startingSeqNo, endingSeqNo] (PEER_RECOVERY origin)
     * </pre>
     *
     * <p>Operations indexed between steps 1 and 3 can arrive twice:
     * <ol>
     *   <li>First via REPLICA replication (step 2) - processed and written to translog</li>
     *   <li>Then via PEER_RECOVERY translog replay (step 4) - duplicate arrival</li>
     * </ol>
     *
     * <p>InternalEngine tolerates this because duplicate translog writes have identical bytes.
     * TSDBEngine cannot because {@code TSDBEngine.rewriteParsedDocumentSource} modifies the document
     * (removes labels from source), so the PEER_RECOVERY version has different bytes,
     * causing {@code TranslogWriter.assertNoSeqNumberConflict} to fail.
     * This assertion is disabled in production,
     * so disabling it for tests is safe and avoids flakiness in TSDBPrimaryRelocationIT test.
     */
    jvmArgs '-da:org.opensearch.index.translog.TranslogWriter'

    // The --debug-jvm command-line option makes the cluster debuggable; this makes the tests debuggable
    if (System.getProperty("test.debug") != null) {
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5005'
    }
}

// Make integTest depend on javaRestTest and internalClusterTest so all integration tests run together
integTest.dependsOn javaRestTest, internalClusterTest

String m3JavaccGrammarPath = 'src/main/java/org/opensearch/tsdb/lang/m3/m3ql/parser'
String m3JavaccOutputPath = 'src/main/java/org/opensearch/tsdb/lang/m3/m3ql/parser/generated'
String promJavaccGrammarPath = 'src/main/java/org/opensearch/tsdb/lang/prom/promql/parser'
String promJavaccOutputPath = 'src/main/java/org/opensearch/tsdb/lang/prom/promql/parser/generated'

javadoc {
  source = sourceSets.main.allJava + fileTree(m3JavaccOutputPath) + fileTree(promJavaccOutputPath)
  options.addStringOption('Xdoclint:all,-missing', '-Xwerror')
  exclude '**/generated/**'
  dependsOn('generateJavaCC')
}


def numNodes = findProperty('numNodes') as Integer ?: 1
def numZones = findProperty('numZones') as Integer ?: 1
def flightEnabled = findProperty('flightEnabled') ?: 'false'
def flightPort = findProperty('flightPort') as Integer ?: 9400

testClusters.integTest {
    testDistribution = "ARCHIVE"

    // Enable feature flags for telemetryPlugin
//    systemProperty 'opensearch.experimental.feature.telemetry.enabled', 'true'
//    setting 'telemetry.feature.metrics.enabled', 'true'

    // Arrow Flight settings
    setting 'tsdb.flight.enabled', flightEnabled
    setting 'tsdb.flight.port', flightPort.toString()

    // Enable the aux transport when Flight is enabled
    if (flightEnabled == 'true') {
        setting 'aux.transport.types', 'tsdb-arrow-flight'
    }

    // JVM args for Arrow memory access (required for Arrow Flight)
    jvmArgs '--add-opens=java.base/java.nio=org.apache.arrow.memory.core,ALL-UNNAMED'

    // Netty settings for Arrow Flight (matching OS core arrow-flight-rpc plugin)
    systemProperty 'io.netty.allocator.numDirectArenas', '1'
    systemProperty 'io.netty.noUnsafe', 'false'
    systemProperty 'io.netty.tryUnsafe', 'true'
    systemProperty 'io.netty.tryReflectionSetAccessible', 'true'

    if (numZones > 1) numberOfZones = numZones
    if (numNodes > 1) numberOfNodes = numNodes

    // This installs our plugin into the testClusters
    plugin(project.tasks.bundlePlugin.archiveFile)
}

testClusters.javaRestTest {
    testDistribution = "ARCHIVE"

    if (numZones > 1) numberOfZones = numZones
    if (numNodes > 1) numberOfNodes = numNodes

    // This installs our plugin into the testClusters
    plugin(project.tasks.bundlePlugin.archiveFile)
}


run {
    useCluster testClusters.integTest
}

// JVM args and system properties for Arrow (matching OS core arrow-flight-rpc plugin)
tasks.withType(Test).configureEach {
    jvmArgs '--add-opens=java.base/java.nio=org.apache.arrow.memory.core,ALL-UNNAMED'
    systemProperty 'io.netty.allocator.numDirectArenas', '1'
    systemProperty 'io.netty.noUnsafe', 'false'
    systemProperty 'io.netty.tryUnsafe', 'true'
    systemProperty 'io.netty.tryReflectionSetAccessible', 'true'
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                '**/generated/**'
            ])
        }))
    }
}
test.finalizedBy(project.tasks.jacocoTestReport)
test.finalizedBy(project.tasks.jacocoTestCoverageVerification)

jacocoTestCoverageVerification {
    dependsOn compileJava, processResources
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                '**/generated/**'
            ])
        }))
    }
    violationRules {
        rule {
            limit {
                minimum = 0.85
            }
        }
    }
}


tasks.named("precommit").configure { dependsOn("spotlessApply", "javadoc") }
tasks.named("check").configure { dependsOn(jacocoTestCoverageVerification) }

/**********************************************
 *            JavaCC Configuration            *
 **********************************************/

configurations {
    javacc
}

dependencies {
    javacc 'net.java.dev.javacc:javacc:7.0.13'
    implementation "${opensearch_group}.plugin:store-subdirectory:${opensearch_version}"
    implementation 'com.google.re2j:re2j:1.7'

    // Arrow Flight dependencies for Flight server POC
    // Versions aligned with OpenSearch 3.5.0: arrow=18.1.0, netty=4.2.9.Final, grpc=1.75.0
    def arrowVersion = '18.1.0'
    def nettyVersion = '4.2.9.Final'
    def grpcVersion = '1.75.0'

    // Arrow memory with Netty allocator (matching OS core arrow-flight-rpc plugin)
    implementation "org.apache.arrow:arrow-memory-netty:${arrowVersion}"
    implementation "org.apache.arrow:arrow-memory-core:${arrowVersion}"
    implementation "org.apache.arrow:arrow-memory-netty-buffer-patch:${arrowVersion}"
    implementation "org.apache.arrow:arrow-vector:${arrowVersion}"
    implementation "org.apache.arrow:arrow-format:${arrowVersion}"
    implementation "org.apache.arrow:flight-core:${arrowVersion}"
    implementation 'com.google.flatbuffers:flatbuffers-java:2.0.0'

    // Jackson (required by Arrow)
    implementation 'com.fasterxml.jackson.core:jackson-core:2.20.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.20'
    implementation 'commons-codec:commons-codec:1.16.1'

    // gRPC dependencies (required by Arrow Flight)
    implementation "io.grpc:grpc-api:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    runtimeOnly "io.grpc:grpc-core:${grpcVersion}"
    runtimeOnly "io.grpc:grpc-protobuf-lite:${grpcVersion}"
    runtimeOnly "io.grpc:grpc-protobuf:${grpcVersion}"
    runtimeOnly 'com.google.code.findbugs:jsr305:3.0.2'

    // Guava (required by gRPC)
    runtimeOnly 'com.google.guava:failureaccess:1.0.1'
    runtimeOnly('com.google.guava:guava:33.3.1-jre') {
        attributes {
            attribute(Attribute.of('org.gradle.jvm.environment', String), 'standard-jvm')
        }
    }

    // Perfmark (required by gRPC at runtime)
    runtimeOnly 'io.perfmark:perfmark-api:0.27.0'

    // Netty dependencies (matching OS core arrow-flight-rpc plugin)
    implementation "io.netty:netty-buffer:${nettyVersion}"
    implementation "io.netty:netty-common:${nettyVersion}"
    implementation "io.netty:netty-codec-base:${nettyVersion}"
    implementation "io.netty:netty-codec-compression:${nettyVersion}"
    implementation "io.netty:netty-codec:${nettyVersion}"
    implementation "io.netty:netty-codec-http:${nettyVersion}"
    implementation "io.netty:netty-codec-http2:${nettyVersion}"
    implementation "io.netty:netty-handler:${nettyVersion}"
    implementation "io.netty:netty-resolver:${nettyVersion}"
    implementation "io.netty:netty-transport:${nettyVersion}"
    implementation "io.netty:netty-transport-native-unix-common:${nettyVersion}"
    implementation "io.netty:netty-transport-classes-epoll:${nettyVersion}"
    implementation "io.netty:netty-tcnative-classes:2.0.66.Final"

    // SLF4J API and Log4j2 bridge (required by Arrow)
    implementation 'org.slf4j:slf4j-api:2.0.9'
    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j2-impl:2.21.1'

    // JOL (Java Object Layout) for memory layout testing
    testImplementation 'org.openjdk.jol:jol-core:0.17'

    // Jackson dependencies for YAML and JSON processing in test framework
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
    testImplementation 'com.fasterxml.jackson.core:jackson-annotations:2.20'
    testImplementation 'com.fasterxml.jackson.core:jackson-core:2.20.1'
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.20.1'

    // javaRestTest dependencies
    javaRestTestImplementation 'com.fasterxml.jackson.core:jackson-core:2.20.1'
    javaRestTestImplementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
    javaRestTestImplementation 'com.fasterxml.jackson.core:jackson-annotations:2.20'
    javaRestTestImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.20.1'

    // internalClusterTest dependencies
    internalClusterTestImplementation 'com.fasterxml.jackson.core:jackson-core:2.20.1'
    internalClusterTestImplementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
    internalClusterTestImplementation 'com.fasterxml.jackson.core:jackson-annotations:2.20'
    internalClusterTestImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.20.1'

}

tasks.register("cleanJavaCCGenerated", Delete) {
    delete fileTree(m3JavaccOutputPath) {
        include '*.java'
    }
    delete fileTree(promJavaccOutputPath) {
        include '*.java'
    }
}

tasks.register("generateM3JavaCC", JavaExec) {
    mainClass = 'javacc'
    classpath = configurations.javacc
    systemProperty 'file.encoding', 'UTF-8'
    args '-OUTPUT_DIRECTORY=' + m3JavaccOutputPath,
            "${file(m3JavaccGrammarPath)}/m3ql.jj"
}

tasks.register("generatePromJavaCC", JavaExec) {
    mainClass = 'javacc'
    classpath = configurations.javacc
    systemProperty 'file.encoding', 'UTF-8'
    args '-OUTPUT_DIRECTORY=' + promJavaccOutputPath,
            "${file(promJavaccGrammarPath)}/promql.jj"
}

tasks.register("generateJavaCC") {
    dependsOn "cleanJavaCCGenerated", "generateM3JavaCC", "generatePromJavaCC", "spotlessApply", "spotlessCheck"
}

compileJava.dependsOn generateJavaCC

// Exclude store-subdirectory from dependency license checks since it's an internal OpenSearch plugin
tasks.named("dependencyLicenses").configure {
    ignoreSha('store-subdirectory')
}

// Exclude generated files from forbidden APIs check
forbiddenApisMain {
    exclude 'org/opensearch/tsdb/lang/m3/m3ql/parser/generated/**'
    exclude 'org/opensearch/tsdb/lang/prom/promql/parser/generated/**'
}

forbiddenApisTest {
    exclude 'org/opensearch/tsdb/lang/m3/m3ql/parser/generated/**'
    exclude 'org/opensearch/tsdb/lang/prom/promql/parser/generated/**'
}

forbiddenApisJavaRestTest {
    exclude 'org/opensearch/tsdb/lang/m3/m3ql/parser/generated/**'
    exclude 'org/opensearch/tsdb/lang/prom/promql/parser/generated/**'
}

forbiddenApisInternalClusterTest {
    exclude 'org/opensearch/tsdb/lang/m3/m3ql/parser/generated/**'
    exclude 'org/opensearch/tsdb/lang/prom/promql/parser/generated/**'
}

forbiddenPatterns {
    exclude 'org/opensearch/tsdb/lang/m3/m3ql/parser/generated/**'
    exclude 'org/opensearch/tsdb/lang/prom/promql/parser/generated/**'
}
